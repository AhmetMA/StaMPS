#!/bin/csh -f

# Interface between Gamma and MAINSAR
#
# Andy Hooper, December 2012
#
# ===========================================================================
# Expected directory structure:
# rslc/*.rslc
# rslc/*.rslc.par
# diff0/*.diff
# geo/*dem.rdc
# geo/*diff_par
# dem/*_seg.par
# ===========================================================================

### Initial welcome
set PRG = `basename "$0"`
set AUT = "Andy Hooper, December 2012"
echo "$PRG $AUT"
echo " "
set nonomatch

if ($#argv < 2) then
    echo "usage: mt_prep_gamma yyyymmdd rslcdir da_thresh [rg_patches az_patches rg_overlap az_overlap]"
    echo "    yyyymmdd                 = master date"
    echo "    rslcdir                  = directory of resampled slcs"
    echo "    da_thresh                = (delta) amplitude dispersion threshold"
    echo "                                typical values: 0.4 for PS, 0.6 for SB"
    echo "    rg_patches (default 1)   = number of patches in range"
    echo "    az_patches (default 1)   = number of patches in azimuth"
    echo "    rg_overlap (default 50)  = overlapping pixels between patches in range"
    echo "    az_overlap (default 50) = overlapping pixels between patches in azimuth"
    echo "    maskfile (optional) char file, same dimensions as slcs, 0 to include, 1 otherwise"
    echo ""
    exit(4)
endif

set master = $argv[1]
set rslcdir = $argv[2]

if ($#argv > 2) then
    set da_thresh = $argv[3]
else 
    set da_thresh = 0.4
endif

if ($#argv > 3) then
    set prg = $argv[4]
else 
    set prg = 1
endif

if ($#argv > 4) then
    set paz = $argv[5]
else 
    set paz = 1
endif

if ($#argv > 5) then
    set overlap_rg = $argv[6]
else 
    set overlap_rg = 50
endif

if ($#argv > 6) then
    set overlap_az = $argv[7]
else 
    set overlap_az = 50
endif

if ($#argv > 7) then
    set maskfile = $argv[8]
    if (! -e $maskfile) then
       echo $maskfile " does not exist, exiting"
       exit 2
    endif
else 
    set maskfile = ""
endif

echo "Amplitude Dispersion Threshold:" $da_thresh
echo "Processing" $prg "patch(es) in range and" $paz "in azimuth"
echo " "

set WORKDIR = $cwd
set dirname = `echo $WORKDIR | gawk 'BEGIN {FS = "/"}{print $(NF)}'`


if ($dirname == "SMALL_BASELINES") then
    set INSARDIR = `echo $WORKDIR | gawk 'BEGIN {FS = "/SMALL_BASELINES"}{print $1}'`
    echo "Small Baseline Processing"
    echo " "
else
    set INSARDIR = $WORKDIR
endif

set RSC = $rslcdir/$master.*slc.par

set length   = `gawk '/azimuth_lines/ {print $2}'     < $RSC`
set width    = `gawk '/range_samples/ {print $2}'     < $RSC`

echo "gamma" > processor.txt
if ($dirname == "SMALL_BASELINES") then
    matlab -nojvm -nosplash -nodisplay < $STAMPS/matlab/sb_parms_initial.m > sb_parms_initial.log
else
    matlab -nojvm -nosplash -nodisplay < $STAMPS/matlab/ps_parms_initial.m > ps_parms_initial.log
endif

echo $width > $WORKDIR/width.txt
echo $length > $WORKDIR/len.txt
echo $RSC > $WORKDIR/rsc.txt

\rm -f $WORKDIR/calamp.in >& /dev/null
\ls $rslcdir/*.*slc >> $WORKDIR/calamp.in
set selfile = $WORKDIR/selpsc.in
calamp calamp.in $width $WORKDIR/calamp.out s 1 $maskfile

# Set up patches
echo $da_thresh > $selfile
echo $width >> $selfile
cat $WORKDIR/calamp.out >> $selfile

@ width_p = $width / $prg
echo $width_p
@ length_p = $length / $paz
echo $length_p
set irg = 0
set iaz = 0
set ip = 0
\rm -rf PATCH_* >& /dev/null
\rm -rf patch.list
while ($irg < $prg)
    @ irg = $irg + 1
    while ($iaz < $paz)
        @ iaz = $iaz + 1
        @ ip = $ip + 1
        @ start_rg1 = $width_p * ($irg - 1) + 1
        @ start_rg = $start_rg1 - $overlap_rg
        if ($start_rg < 1) then
            set start_rg = 1
        endif
        @ end_rg1 = $width_p * $irg
        @ end_rg = $end_rg1 + $overlap_rg
        if ($end_rg > $width) then
            @ end_rg = $width
        endif
        @ start_az1 = $length_p * ($iaz - 1) + 1
        @ start_az = $start_az1 - $overlap_az
        if ($start_az < 1) then
            set start_az = 1
        endif
        @ end_az1 = $length_p * $iaz
        @ end_az = $end_az1 + $overlap_az

        if ($end_az > $length) then
            @ end_az = $length
        endif

        if (! -e PATCH_$ip) then
            mkdir PATCH_$ip
        endif

        echo PATCH_$ip >> patch.list

        cd PATCH_$ip
        echo $start_rg > patch.in
        echo $end_rg >> patch.in
        echo $start_az >> patch.in
        echo $end_az >> patch.in
        echo $start_rg1 > patch_noover.in
        echo $end_rg1 >> patch_noover.in
        echo $start_az1 >> patch_noover.in
        echo $end_az1 >> patch_noover.in
        cd ..
    end
    set iaz = 0
end


echo $width > pscphase.in
\ls $rslcdir/../diff0/*.diff >> pscphase.in

#echo $width > psclonlat.in
#echo $rslcdir'../geo/*.*' >> psclonlat.in

echo $width > pscdem.in
\ls $rslcdir/../geo/*dem.rdc >> pscdem.in



#\ls -d PATCH_* > patch.list
mt_extract_cands 1 0 1 1 s 1 $maskfile

set dempar = `\ls $rslcdir/../dem/*_seg.par`
set diffpar = `\ls $rslcdir/../geo/*.diff_par`
echo $dempar
echo $diffpar

foreach patch(`\cat patch.list`)

    cd $patch
    echo "Patch:" $patch

    #Retrieve lon/lat for PS candidates
    pt2geo pscands.1.ij.int - $RSC - pscands.1.hgt $dempar $diffpar 1 1  pt.map pscands.1.xy pscands.1.ll
end
