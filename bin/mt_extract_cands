#!/bin/csh -f

# Cycle through patches in patch.list and extract data for
# candidiate pixels 
#
# Andy Hooper, Jan 2007

### Initial welcome
set PRG = `basename "$0"`
set AUT = "Andy Hooper, Jan 2007"
echo "$PRG $AUT"
echo " "

if ($#argv > 0) then
    set phase_only = $argv[1]
else 
    set phase_only = 0
endif

set WORKDIR = $cwd
set dirname = `echo $WORKDIR | gawk 'BEGIN {FS = "/"}{print $(NF)}'`

foreach patch(`\cat patch.list`)

    cd $patch
    echo "Patch:" $patch

    if ($phase_only == 0) then
        # Select PS candidates 
        if ($dirname == "SMALL_BASELINES") then
            selsbc_patch $WORKDIR/selsbc.in patch.in pscands.1.ij pscands.1.da 
        else
            selpsc_patch $WORKDIR/selpsc.in patch.in pscands.1.ij pscands.1.da 
        endif

        # Retrieve lon/lat for PS candidates
        psclonlat $WORKDIR/psclonlat.in pscands.1.ij pscands.1.ll

        # Retrieve hgt for PS candidates
        pscdem $WORKDIR/pscdem.in pscands.1.ij pscands.1.hgt
    endif

    # Retrieve phase for PS candidates
    pscphase $WORKDIR/pscphase.in pscands.1.ij pscands.1.ph


    cd $WORKDIR
end


